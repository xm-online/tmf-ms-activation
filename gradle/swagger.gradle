/*
 * Plugin that provides API-first development using OpenAPI-generator to
 * generate Spring-MVC endpoint stubs at compile time from an OpenAPI definition file
 */
buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        // Use your property if present, otherwise a sane default
        classpath "org.openapitools:openapi-generator-gradle-plugin:" +
            (project.hasProperty("openapiPluginVersion") ? project.property("openapiPluginVersion") : "7.1.0")
    }
}

apply plugin: "org.openapi.generator"

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

tasks.register("openApiGenerateV2", GenerateTask) {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/swagger/api.yml".toString()
    outputDir = "$buildDir/openapi".toString()
    apiPackage = "com.icthh.xm.tmf.ms.activation.api"
    modelPackage = "com.icthh.xm.tmf.ms.activation.model"
    apiFilesConstrainedTo = [""]
    modelFilesConstrainedTo = [""]
    supportingFilesConstrainedTo = ["ApiUtil.java"]
    configOptions = [delegatePattern: "true", title: "activation", useSpringBoot3: "true", useJakartaEe: "true"]
    validateSpec = true
}

tasks.register("openApiGenerateV4", GenerateTask) {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/swagger/api_v4.yml".toString()
    outputDir = "$buildDir/openapi".toString()
    apiPackage = "com.icthh.xm.tmf.ms.activation.api.v4"
    modelPackage = "com.icthh.xm.tmf.ms.activation.model.v4"
    apiFilesConstrainedTo = [""]
    modelFilesConstrainedTo = [""]
    supportingFilesConstrainedTo = ["ApiUtil.java"]
    configOptions = [delegatePattern: "true", title: "activation", useSpringBoot3: "true", useJakartaEe: "true"]
    validateSpec = true
    additionalProperties = [useTags: "true"]
}

sourceSets {
    main {
        java {
            srcDir file("${project.buildDir.path}/openapi/src/main/java")
        }
    }
}

compileJava.dependsOn("openApiGenerateV2", "openApiGenerateV4")
